let Cat =
      ./../Cat/semigroupal sha256:de5b05c9640ea3ca0ee586c7102a982db79d3c0f0c92230cb2df19c7b986b513

let vObject = Type

let v =
      ./../Monoidal/Cartesian/extractMonoidal sha256:0fcc63fc837459ca2866d31c5b11400b56fcb8f7d1846b774e98b47995c1879a
      Cat
      Type
      vObject
      ./monoidal/cartesian sha256:b6efbdc0effbca1bc7427c980f2579012808494a8195fdf9fb52aea37ae91116

let Set =
      ./../Monoidal/Cartesian/extractSemigroupal sha256:5ea36ef859f12ab3575e578d19b38bfd9b50e7540a62cad09b80504653da57e6
      Cat
      vObject
      Type
      ./monoidal/cartesian sha256:b6efbdc0effbca1bc7427c980f2579012808494a8195fdf9fb52aea37ae91116

in    { split =
            λ(a : Type)
          → λ(b : Type)
          → λ(c : Type)
          → λ(d : Type)
          → λ(f : a → b)
          → λ(g : c → d)
          → λ(p : { _1 : a, _2 : c })
          → { _1 = f p._1, _2 = g p._2 }
      , fanout =
            λ(a : Type)
          → λ(b : Type)
          → λ(c : Type)
          → λ(f : a → b)
          → λ(g : a → c)
          → λ(x : a)
          → { _1 = f x, _2 = g x }
      , identity =
          λ(a : Type) → λ(x : a) → x
      , product =
            λ(a : { _1 : Type, _2 : Type })
          → λ(p : ∀(r : Type) → (∀(z : Type) → (z → a._2) → (a._1 → z) → r) → r)
          → p
            (a._1 → a._2)
            (   λ(z : Type)
              → λ(f : z → a._2)
              → λ(g : a._1 → z)
              → λ(x : a._1)
              → f (g x)
            )
      , lmap =
            λ(a : Type)
          → λ(b : Type)
          → λ(c : Type)
          → λ(f : b → a)
          → λ(p : a → c)
          → λ(x : b)
          → p (f x)
      , rmap =
            λ(a : Type)
          → λ(b : Type)
          → λ(c : Type)
          → λ(f : b → c)
          → λ(p : a → b)
          → λ(x : a)
          → f (p x)
      , map =
            λ(a : { _1 : Type, _2 : Type })
          → λ(b : { _1 : Type, _2 : Type })
          → λ(f : { _1 : b._1 → a._1, _2 : a._2 → b._2 })
          → λ(fn : a._1 → a._2)
          → λ(x : b._1)
          → f._2 (fn (f._1 x))
      }
    : ./../../Arrow/Type sha256:e2050044f4f51e126b329c08e88ad827636c005cf4111d5847558d19c0512759
      Type
      v
      Set
