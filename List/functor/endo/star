let kArrow =
      ./../../../Function/Kind sha256:0b6372c593badac5fe880699fd1cf19dfba07c62fab935a8bad96915866f466e

let object = Type

let cat =
      ./../../../Category/Set/monoidal/cartesian sha256:b3c97e385d62b718ab11e540a98141cb81b89db0d7c1115ec11b330c596dcf01

in    λ ( m
        : kArrow object object
        )
    → λ ( monoidal
        : ./../../../Functor/Monoidal/Type sha256:bf47360872158ca906f90c060e3a3339c2ca60a630baa0446884e840ca7fbf70
          object
          cat
          cat
          m
        )
    →   { map =
              λ ( a
                : object
                )
            → λ(b : object)
            → λ(f : cat.arrow { _1 = a, _2 = m b })
            → λ(fa : List a)
            → let liftCons =
                      λ ( mb
                        : m b
                        )
                    → λ(mlb : m (List b))
                    → monoidal.op
                      (List b)
                      (   λ ( r
                            : Type
                            )
                        → λ ( day
                            : ./../../../Day/convolution sha256:dffad22f69e29116debfd7702a83fae5a1278fa46a80668aa9730c28c1afdd02
                              object
                              cat
                              cat
                              m
                              m
                              (List b)
                              r
                            )
                        → day
                          { _1 = b, _2 = List b }
                          (λ(p : { _1 : b, _2 : List b }) → [ p._1 ] # p._2)
                          { _1 = mb, _2 = mlb }
                      )
              
              in  List/fold
                  a
                  fa
                  (m (List b))
                  (λ(x : a) → liftCons (f x))
                  ( monoidal.identity
                    (List b)
                    (λ(_ : cat.identity) → [] : List b)
                  )
        }
      : ./../../../Functor/Endo/Star/Type sha256:bda0cae973d2e4f314fc165f8b7cd96d87f642bc9ce2b22569cac97346068717
        object
        cat.arrow
        m
        List
