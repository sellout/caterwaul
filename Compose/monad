let kArrow = ./../Function/Kind

let kProduct = ./../Tuple/Kind

let vObject = Type

let v =
      ./../Category/Monoidal/extractCategory
      kArrow
      kProduct
      Type
      vObject
      ./../Category/Set/monoidal/cartesian

let Category = ./../Category/Kind kArrow kProduct vObject

in    λ(cObject : Kind)
    → λ(c : Category cObject)
    → λ(dObject : Kind)
    → λ(d : Category dObject)
    → λ(f : kArrow dObject cObject)
    → λ(g : kArrow cObject dObject)
    → λ(gFunctor : ./../Functor/Type vObject cObject dObject v c d g)
    → λ(adjunction : ./../Adjunction/Type cObject dObject c d f g)
    →   { identity =
            adjunction.unit
        , op =
              λ(a : dObject)
            → gFunctor.map (f (g (f a))) (f a) (adjunction.counit (f a))
        }
      : ./../Monad/Type
        dObject
        d
        (./Type dObject cObject dObject { _1 = g, _2 = f })
