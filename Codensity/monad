let kCat =
      ./../Category/Cat/semigroupal sha256:de5b05c9640ea3ca0ee586c7102a982db79d3c0f0c92230cb2df19c7b986b513

let vObject = Type

let v =
      ./../Category/Monoidal/Cartesian/extractMonoidal sha256:0fcc63fc837459ca2866d31c5b11400b56fcb8f7d1846b774e98b47995c1879a
      kCat
      Type
      vObject
      ./../Category/Set/monoidal/cartesian sha256:b6efbdc0effbca1bc7427c980f2579012808494a8195fdf9fb52aea37ae91116

let object = Type

let cat =
      ./../Category/Monoidal/Cartesian/extractCategory sha256:fef093587bdc97a914e4e30469579c22f8fb63653ea5f511606e0e8109662a4b
      kCat
      vObject
      object
      ./../Category/Set/monoidal/cartesian sha256:b6efbdc0effbca1bc7427c980f2579012808494a8195fdf9fb52aea37ae91116

in    λ ( category
        : ./../Category/Type sha256:a3968397f7c9a309d7c4eed545517a4e6f451eff463aaaf274fd1c7d37cb3004
          object
          v
          cat
        )
    → let Codensity =
            ./Type sha256:b0945f99031bc91ed9a0bc7c25b48ef903bf0c3746ac6fcaaeb04fefa0b652e1
            object
            cat
      
      in    λ ( m
              : kCat.arrow object object
              )
          →   { identity =
                    λ(a : object)
                  → λ(x : a)
                  → λ(b : object)
                  → λ(k : cat.arrow { _1 = a, _2 = m b })
                  → k x
              , product =
                    λ(a : object)
                  → λ(codensity : Codensity m (Codensity m a))
                  → λ(c : object)
                  → λ(l : cat.arrow { _1 = a, _2 = m c })
                  → codensity
                    c
                    (   λ(x : Codensity m a)
                      → category.identity (Codensity m a) x c l
                    )
              }
            : ./../Monad/Type sha256:0324dd40fc192581e566136f5cde4424ed653ecf7311b967b7d291d05484b78e
              object
              cat
              (Codensity m)
