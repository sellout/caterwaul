let kCat =
      ./../../../Category/Cat/semigroupal sha256:de5b05c9640ea3ca0ee586c7102a982db79d3c0f0c92230cb2df19c7b986b513

let vObject = Type

let v =
      ./../../../Category/Monoidal/Cartesian/extractCategory sha256:fef093587bdc97a914e4e30469579c22f8fb63653ea5f511606e0e8109662a4b
      kCat
      Type
      vObject
      ./../../../Category/Set/monoidal/cartesian sha256:b6efbdc0effbca1bc7427c980f2579012808494a8195fdf9fb52aea37ae91116

let object = Type

let Set =
      ./../../../Category/Set/rig sha256:99e5c0b51dc280856faa2d331627ba6ace4b7d7b278ec47f8a5844beb1c08052

let cat =
      ./../../../Category/Rig/extractMultiplicative sha256:fcce2925fbebe635a50151f4cd5e00b0807811c62395c6fa5c8a87a40b229f32
      kCat
      vObject
      object
      Set

let Semigroup =
      ./../../../Semigroup/Type sha256:c141f4fb3c393804963be5ed72ffe07f5a7e90f588ead9065235240815a80c88
      object
      ( ./../../../Category/Monoidal/extractSemigroupal sha256:a5bbf4cdc598ec13d870022473eb80feeee8f9b2b10528f5df7f48edccb2c751
        kCat
        vObject
        object
        cat
      )

in    λ ( a
        : object
        )
    → λ(semigroup : Semigroup a)
    →     ./../endo/star sha256:b87eef622146c7b43b2a16e492bc30bd03fdfedf037ed9ceebcf94f022500293
          ( ./../../../Identity/Type sha256:0d22fa254c96551228894f2adaedd4f45c0401759d18775e412ae41694f1d3e0
            object
          )
          ./../../../Identity/functor/monoidal sha256:40c429fedb0d4632284b3665f35b06c09b8eb582fc39f6997676aa403a6efc1f
          a
        ∧ { identity =
                λ(b : object)
              → λ(fn : Set.arrow { _1 = Set.multiplicative.identity, _2 = b })
              → < Right = fn {=} | Left : a >
          , product =
                λ(i : object)
              → λ ( day
                  :   ∀(r : Type)
                    → (   ∀(d : { _1 : object, _2 : object })
                        → Set.arrow
                          { _1 = Set.multiplicative.product d, _2 = i }
                        → { _1 :
                              Set.additive.product { _1 = a, _2 = d._1 }
                          , _2 :
                              Set.additive.product { _1 = a, _2 = d._2 }
                          }
                        → r
                      )
                    → r
                  )
              → day
                (Set.additive.product { _1 = a, _2 = i })
                (   λ(p : { _1 : object, _2 : object })
                  → λ ( fn
                      : Set.arrow { _1 = Set.multiplicative.product p, _2 = i }
                      )
                  → λ ( args
                      : { _1 :
                            Set.additive.product { _1 = a, _2 = p._1 }
                        , _2 :
                            Set.additive.product { _1 = a, _2 = p._2 }
                        }
                      )
                  → merge
                    { Left =
                          λ(l : a)
                        → merge
                          { Left =
                                λ(ll : a)
                              → < Left =
                                    semigroup.product { _1 = l, _2 = ll }
                                | Right :
                                    i
                                >
                          , Right =
                              λ(_ : p._2) → < Left = l | Right : i >
                          }
                          args._2
                    , Right =
                          λ(f : Set.arrow { _1 = p._2, _2 = i })
                        → merge
                          { Left =
                              λ(x : a) → < Left = x | Right : i >
                          , Right =
                              λ(x : p._2) → < Right = f x | Left : a >
                          }
                          args._2
                    }
                    ( merge
                      { Left =
                          λ(x : a) → < Left = x | Right : p._2 → i >
                      , Right =
                            λ(x : p._1)
                          → < Right =
                                λ(y : p._2) → fn { _1 = x, _2 = y }
                            | Left :
                                a
                            >
                      }
                      args._1
                    )
                )
          }
      : ./../../../Functor/Monoidal/Type sha256:73d6a61a626107cf8fc9ee73e02b3116a2540bae6fa38881165eab9d8c0f8913
        object
        v
        cat
        cat
        (λ(b : object) → Set.additive.product { _1 = a, _2 = b })
