let cat =
      ./../../Category/Cat/semigroupal sha256:de5b05c9640ea3ca0ee586c7102a982db79d3c0f0c92230cb2df19c7b986b513

let Category =
      ./../../Category/Kind sha256:c73426d12cddcdfc44b6e687fc897f7ef91fcda0c4da116a91acdca4d15c68a2
      cat

let Functor =
      ./../../Functor/Type sha256:bebd999f32d6a3fd9bd7151ca5f0942ed6aeded30809c4a7e6c91382bed2dbcb

in    λ ( vObject
        : Kind
        )
    → λ(v : Category Type vObject)
    → λ ( vCategory
        : ./../../Category/Type sha256:5792bd3a482ad0d318aecda464bd952a6561dc1119bdc1f5db574cfc3b66e095
          vObject
          ( ./../../Category/Monoidal/Cartesian/extractMonoidal sha256:25afdf2818357f9c4b9dac6652b82e730472c389a634f8417a6014d39c7cddc0
            cat
            Type
            Type
            ./../../Category/Set/monoidal/cartesian sha256:48e8bea552638624a62335eab92acd1e0a04c28e8a0d8fe1c8974565065a9576
          )
          v
        )
    → λ(cObject : Kind)
    → λ(c : Category vObject cObject)
    → λ(dObject : Kind)
    → λ(d : Category vObject dObject)
    → λ(eObject : Kind)
    → λ(e : Category vObject eObject)
    → λ(f : cat.arrow dObject eObject)
    → λ(fFunctor : Functor vObject dObject eObject v d e f)
    → λ(g : cat.arrow cObject dObject)
    → λ(gFunctor : Functor vObject cObject dObject v c d g)
    →   { map =
              λ(a : cObject)
            → λ(b : cObject)
            → vCategory.op
              { _1 =
                  c.arrow { _1 = a, _2 = b }
              , _2 =
                  e.arrow { _1 = f (g a), _2 = f (g b) }
              }
              (   λ(r : Type)
                → λ ( arrowsOut
                    :   ∀(z : vObject)
                      → v.arrow
                        { _1 = z, _2 = e.arrow { _1 = f (g a), _2 = f (g b) } }
                      → v.arrow { _1 = c.arrow { _1 = a, _2 = b }, _2 = z }
                      → r
                    )
                → arrowsOut
                  (d.arrow { _1 = g a, _2 = g b })
                  (fFunctor.map (g a) (g b))
                  (gFunctor.map a b)
              )
        }
      : Functor
        vObject
        cObject
        eObject
        v
        c
        e
        ( ./../Type sha256:e830545569ce8ba04d3124a7ca93994fc93e18c0372616f45910253f0a00907d
          cObject
          dObject
          eObject
          { _1 = f, _2 = g }
        )
