let kArrow =
      ./../Function/Kind sha256:0b6372c593badac5fe880699fd1cf19dfba07c62fab935a8bad96915866f466e

let kProduct =
      ./../Tuple/Kind sha256:03e1c52890f0cda1a2181cd3eb045d5344fc7c907fcbf836736c0e3222c5ffd8

in    λ ( cObject
        : Kind
        )
    → λ ( c
        : ./../Category/Monoidal/Kind sha256:c90d64b23bd9269a630ed0e3c5202fef1969034543a57c20db10215d7092fe22
          kArrow
          kProduct
          cObject
        )
    → λ ( semigroupoid
        : ./../Semigroupoid/Type sha256:7ea3ed050672efbe97058fdf8cee2c448261528fb9df88e77792684384125059
          cObject
          c.arrow
        )
    → let vObject =
            Type
      
      let v =
            ./../Category/Set/monoidal/cartesian sha256:b3c97e385d62b718ab11e540a98141cb81b89db0d7c1115ec11b330c596dcf01
      
      let Day =
            ./Type sha256:681b4b22f4cd4484c75a93d75c4c4a58c7d7b146d8f9e1a52602b49a8fb51f87
            cObject
            c
            v
      
      in    λ ( f
              : kArrow cObject vObject
              )
          → λ(g : kArrow cObject vObject)
          →   { map =
                    λ ( a
                      : cObject
                      )
                  → λ(b : cObject)
                  → λ(fn : c.arrow { _1 = a, _2 = b })
                  → λ(day : Day { _1 = f, _2 = g } a)
                  → day
                    (Day { _1 = f, _2 = g } b)
                    (   λ ( d
                          : { _1 : cObject, _2 : cObject }
                          )
                      → λ(fn2 : c.arrow { _1 = c.op d, _2 = a })
                      → λ(e : v.op { _1 = f d._1, _2 = g d._2 })
                      → λ(r0 : vObject)
                      → λ ( day0
                          : ./convolution sha256:dffad22f69e29116debfd7702a83fae5a1278fa46a80668aa9730c28c1afdd02
                            cObject
                            c
                            v
                            f
                            g
                            b
                            r0
                          )
                      → day0
                        d
                        ( semigroupoid.op
                          { _1 = c.op d, _2 = b }
                          (   λ(r : Type)
                            → λ ( arrowsOut
                                :   ∀(z : cObject)
                                  → c.arrow { _1 = z, _2 = b }
                                  → c.arrow { _1 = c.op d, _2 = z }
                                  → r
                                )
                            → arrowsOut a fn fn2
                          )
                        )
                        e
                    )
              }
            : ./../Functor/Type sha256:fe94261bd581198ae1523ffbe7037159ad8327199b42ee3f97d16a7e1fa1dc79
              cObject
              vObject
              c.arrow
              v.arrow
              (Day { _1 = f, _2 = g })
