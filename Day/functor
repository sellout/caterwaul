let kCat =
      ./../Category/Cat/semigroupal sha256:de5b05c9640ea3ca0ee586c7102a982db79d3c0f0c92230cb2df19c7b986b513

let vObject = Type

let v =
      ./../Category/Monoidal/Cartesian/extractMonoidal sha256:25afdf2818357f9c4b9dac6652b82e730472c389a634f8417a6014d39c7cddc0
      kCat
      Type
      vObject
      ./../Category/Set/monoidal/cartesian sha256:48e8bea552638624a62335eab92acd1e0a04c28e8a0d8fe1c8974565065a9576

let base =
      ./../Category/Monoidal/extractCategory sha256:936b393338b8dcf60af6c25ecea2b0b02d83e18acfed60ff736754fe0c1d7b3d
      kCat
      vObject

in    λ ( cObject
        : Kind
        )
    → λ ( c
        : ./../Category/Monoidal/Kind sha256:6eb37e632903173c5ec1600564aa614697c4b58719773c4e4c66a76f095d1636
          kCat
          vObject
          cObject
        )
    → λ ( semigroupoid
        : ./../Semigroupoid/Type sha256:c43adcdb5e9cb39774277d7dcbeb77a8c85a6d64458b129d1a92bac3711f132d
          cObject
          v
          (base cObject c)
          c.arrow
        )
    → let Day =
            ./Type sha256:75bb9592fce61b6be1565b8b7589763f51b71a9dab310fd5b9c72dbce176a67f
            cObject
            v
            c
      
      in    λ ( f
              : kCat.arrow cObject vObject
              )
          → λ(g : kCat.arrow cObject vObject)
          →   { map =
                    λ ( a
                      : cObject
                      )
                  → λ(b : cObject)
                  → λ(fn : c.arrow { _1 = a, _2 = b })
                  → λ(day : Day { _1 = f, _2 = g } a)
                  → day
                    (Day { _1 = f, _2 = g } b)
                    (   λ ( d
                          : { _1 : cObject, _2 : cObject }
                          )
                      → λ(fn2 : c.arrow { _1 = c.op d, _2 = a })
                      → λ(e : v.op { _1 = f d._1, _2 = g d._2 })
                      → λ(r0 : vObject)
                      → λ ( day0
                          : ./convolution sha256:ca965ddd64a13ff9661a3e175fa344344e188283b59f6620afb6ffb36fe95ba5
                            cObject
                            v
                            c
                            f
                            g
                            b
                            r0
                          )
                      → day0
                        d
                        ( semigroupoid.op
                          { _1 = c.op d, _2 = b }
                          (   λ(r : Type)
                            → λ ( arrowsOut
                                :   ∀(z : cObject)
                                  → c.arrow { _1 = z, _2 = b }
                                  → c.arrow { _1 = c.op d, _2 = z }
                                  → r
                                )
                            → arrowsOut a fn fn2
                          )
                        )
                        e
                    )
              }
            : ./../Functor/Type sha256:bebd999f32d6a3fd9bd7151ca5f0942ed6aeded30809c4a7e6c91382bed2dbcb
              vObject
              cObject
              vObject
              (base vObject v)
              (base cObject c)
              (base vObject v)
              (Day { _1 = f, _2 = g })
