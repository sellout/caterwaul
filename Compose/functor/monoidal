{- NB: Since this is currently forced to be in *Set*, it’s quite possible that
       some of the uses of _C_ and _D_ are wrong, since the type system can’t
       distinguish them. So take them with a grain of salt if you’re editing
       this file.
-}
let cat =
      ./../../Category/Cat/semigroupal sha256:de5b05c9640ea3ca0ee586c7102a982db79d3c0f0c92230cb2df19c7b986b513

let base =
      ./../../Category/Monoidal/Cartesian/extractMonoidal sha256:8112e73d82fd63e4e8bb5628a8cea43f92518a02e56ad351c7f9558546e7f81a
      cat

let MonoidalFunctor =
      ./../../Functor/Monoidal/Type sha256:bf8111231d2f34c80be8863044f587a617c364172c46684ce6480c5c96764e74

let vObject = Type

in    λ ( cObject
        : Kind
        )
    → λ ( v
        : ./../../Category/Kind sha256:c73426d12cddcdfc44b6e687fc897f7ef91fcda0c4da116a91acdca4d15c68a2
          cat
          Type
          vObject
        )
    → λ ( vCategory
        : ./../../Category/Type sha256:3983e08568dd06043567236a0b0ceb489d34346ea7cc29bd9c4d98bd567e03b9
          vObject
          ( base
            vObject
            Type
            ./../../Category/Set/monoidal/cartesian sha256:3a79a17d1c39a3df58efcc62fd96a2da0147bb29b524f57aa7c35942f653f164
          )
          v
        )
    → λ ( c
        : ./../../Category/Monoidal/Kind sha256:8e77a076f4c2f6e1d0ad07cfec37dfd04782e1a0a7e98a72a305233f69c494ca
          cat
          vObject
          cObject
        )
    → let dObject =
            Type
      
      let d =
            base
            vObject
            dObject
            ./../../Category/Set/monoidal/cartesian sha256:3a79a17d1c39a3df58efcc62fd96a2da0147bb29b524f57aa7c35942f653f164
      
      let eObject = Type
      
      let e =
            base
            vObject
            eObject
            ./../../Category/Set/monoidal/cartesian sha256:3a79a17d1c39a3df58efcc62fd96a2da0147bb29b524f57aa7c35942f653f164
      
      let Compose =
            ./../Type sha256:e830545569ce8ba04d3124a7ca93994fc93e18c0372616f45910253f0a00907d
            cObject
            dObject
            eObject
      
      let Day =
            ./../../Day/Type sha256:f2b5b6fb9d2fead2e46f0fe8362e08133d5f552c932d9ca03126f256d24b8520
            cObject
            e
            c
      
      let Day/convolution =
            ./../../Day/convolution sha256:ed0886d09259f7be70d35f7735d511b586438cc026f26fcf254b617f9a59610f
      
      in    λ(f : cat.arrow dObject eObject)
          → λ(fMonoidal : MonoidalFunctor dObject v d e f)
          → λ(g : cat.arrow cObject dObject)
          → λ(gMonoidal : MonoidalFunctor cObject v c d g)
          →   { map =
                    λ(a : cObject)
                  → λ(b : cObject)
                  → vCategory.product
                    { _1 =
                        c.arrow { _1 = a, _2 = b }
                    , _2 =
                        e.arrow { _1 = f (g a), _2 = f (g b) }
                    }
                    (   λ(r : Type)
                      → λ ( arrowsOut
                          :   ∀(z : vObject)
                            → v.arrow
                              { _1 =
                                  z
                              , _2 =
                                  e.arrow { _1 = f (g a), _2 = f (g b) }
                              }
                            → v.arrow
                              { _1 = c.arrow { _1 = a, _2 = b }, _2 = z }
                            → r
                          )
                      → arrowsOut
                        (d.arrow { _1 = g a, _2 = g b })
                        (fMonoidal.map (g a) (g b))
                        (gMonoidal.map a b)
                    )
              , unit =
                    λ(a : cObject)
                  → λ(fn : c.arrow { _1 = c.unit, _2 = a })
                  → fMonoidal.unit (g a) (λ(_ : d.unit) → gMonoidal.unit a fn)
              , product =
                    λ(i : cObject)
                  → λ ( day
                      : Day
                        { _1 =
                            Compose { _1 = f, _2 = g }
                        , _2 =
                            Compose { _1 = f, _2 = g }
                        }
                        i
                      )
                  → day
                    (f (g i))
                    (   λ(b : { _1 : cObject, _2 : cObject })
                      → λ(fn : c.arrow { _1 = c.product b, _2 = i })
                      → λ(a : { _1 : f (g b._1), _2 : f (g b._2) })
                      → fMonoidal.product
                        (g i)
                        (   λ(fr : eObject)
                          → λ(fDay : Day/convolution dObject e d f f (g i) fr)
                          → fDay
                            { _1 = g b._1, _2 = g b._2 }
                            (   λ(p : { _1 : g b._1, _2 : g b._2 })
                              → gMonoidal.product
                                i
                                (   λ(gr : dObject)
                                  → λ ( gDay
                                      : Day/convolution cObject d c g g i gr
                                      )
                                  → gDay b fn p
                                )
                            )
                            a
                        )
                    )
              }
            : MonoidalFunctor cObject v c e (Compose { _1 = f, _2 = g })
