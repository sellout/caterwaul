{- In a Leibniz category, unit is refl, and product is transitivity. -}
let cat = ./../Category/Cat/semigroupal

let vObject = Type

in    λ(cObject : Kind)
    → λ(dObject : Kind)
    → λ(v : ./../Category/Monoidal/Kind cat Type vObject)
    → λ(c : ./../Category/Kind cat vObject cObject)
    → λ(d : ./../Category/Kind cat vObject dObject)
    → λ(dCategory : ./../Category/Type dObject v d)
    → let Leibniz = ./Kind cObject dObject c d
      
      in    { unit =
                  λ(a : cObject)
                → { subst =
                      λ(f : cat.arrow cObject dObject) → dCategory.unit (f a)
                  }
            , product =
                  λ(a : { _1 : cObject, _2 : cObject })
                → λ ( p
                    :   ∀(r : Type)
                      → (   ∀(z : cObject)
                          → Leibniz.arrow { _1 = z, _2 = a._2 }
                          → Leibniz.arrow { _1 = a._1, _2 = z }
                          → r
                        )
                      → r
                    )
                → { subst =
                        λ(f : cat.arrow cObject dObject)
                      → dCategory.product
                        { _1 = f a._1, _2 = f a._2 }
                        (   λ(r : Type)
                          → λ ( arrowsOut
                              :   ∀(z : dObject)
                                → d.arrow { _1 = z, _2 = f a._2 }
                                → d.arrow { _1 = f a._1, _2 = z }
                                → r
                              )
                          → p
                            r
                            (   λ(z : cObject)
                              → λ(g : Leibniz.arrow { _1 = z, _2 = a._2 })
                              → λ(h : Leibniz.arrow { _1 = a._1, _2 = z })
                              → arrowsOut (f z) (g.subst f) (h.subst f)
                            )
                        )
                  }
            }
          : ./../Category/Type cObject v Leibniz
