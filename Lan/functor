let kArrow =
      ./../Function/Kind sha256:0b6372c593badac5fe880699fd1cf19dfba07c62fab935a8bad96915866f466e

let kProduct =
      ./../Tuple/Kind sha256:03e1c52890f0cda1a2181cd3eb045d5344fc7c907fcbf836736c0e3222c5ffd8

let base =
      ./../Category/Monoidal/extractCategory sha256:f8fd8dd2ec93dac14e091587db11696f084c1d8eb9de88b71b625151904db9ff
      kArrow
      kProduct

let vObject = Type

let v =
      ./../Category/Set/monoidal/cartesian sha256:b017581ee66eec1995d52c80d527598dd5614254bf7217768c035c3a5e2103bd

in    λ ( cObject
        : Kind
        )
    → λ(cpObject : Kind)
    → λ ( cp
        : ./../Category/Kind sha256:41ea86fd34226d0dc3c87cc12f9ce042e3b54e7eb77b451cdbfb085ba3f66916
          kArrow
          kProduct
          vObject
          cpObject
        )
    → λ ( cpSemigroupoid
        : ./../Semigroupoid/Type sha256:8cb9ba1a061053c5b685c45eefe13ace83aff91ddb0d381f204af51240ff4606
          cpObject
          v
          cp
          cp.arrow
        )
    → let dObject =
            Type
      
      let d =
            base
            vObject
            dObject
            ./../Category/Set/monoidal/cartesian sha256:b017581ee66eec1995d52c80d527598dd5614254bf7217768c035c3a5e2103bd
      
      let Lan =
            ./Type sha256:9ca4462b685f10f495c272b038e14f3057cef70898133772a5ef07dbaf5be0e1
            cObject
            cpObject
            cp
      
      in    λ ( f
              : kArrow cObject cpObject
              )
          → λ(g : kArrow cObject dObject)
          →   { map =
                    λ(a : cpObject)
                  → λ(b : cpObject)
                  → λ(h : cp.arrow { _1 = a, _2 = b })
                  → λ(lan : Lan f g a)
                  → λ(r : Type)
                  → λ ( k
                      :   ∀(c : cObject)
                        → cp.arrow { _1 = f c, _2 = b }
                        → d.arrow { _1 = g c, _2 = r }
                      )
                  → lan
                    r
                    (   λ(c : cObject)
                      → λ(x : cp.arrow { _1 = f c, _2 = a })
                      → k
                        c
                        ( cpSemigroupoid.op
                          { _1 = f c, _2 = b }
                          (   λ(r : Type)
                            → λ ( arrowsOut
                                :   ∀(z : cpObject)
                                  → cp.arrow { _1 = z, _2 = b }
                                  → cp.arrow { _1 = f c, _2 = z }
                                  → r
                                )
                            → arrowsOut a h x
                          )
                        )
                    )
              }
            : ./../Functor/Type sha256:9222d1a669a235e545d7d24bc2845509339fb9f17711afef958df74457784d4d
              vObject
              cpObject
              dObject
              (base Type vObject v)
              cp
              d
              (Lan f g)
