let kArrow = ./../Function/Kind

let kProduct = ./../Tuple/Kind

let base = ./../Category/Monoidal/extractCategory kArrow kProduct

let vObject = Type

let v = ./../Category/Set/monoidal/cartesian

in    λ(cObject : Kind)
    → λ(cpObject : Kind)
    → λ(cp : ./../Category/Kind kArrow kProduct vObject cpObject)
    → λ(cpSemigroupoid : ./../Semigroupoid/Type cpObject v cp cp.arrow)
    → let dObject = Type
      
      let d = base vObject dObject ./../Category/Set/monoidal/cartesian
      
      let Ran = ./Type cObject cpObject cp
      
      in    λ(p : kArrow cObject cpObject)
          → λ(f : kArrow cObject dObject)
          →   { map =
                    λ(a : cpObject)
                  → λ(b : cpObject)
                  → λ(fn : cp.arrow { _1 = a, _2 = b })
                  → λ(ran : Ran p f a)
                  → λ(c : cObject)
                  → λ(k : cp.arrow { _1 = b, _2 = p c })
                  → ran
                    c
                    ( cpSemigroupoid.op
                      { _1 = a, _2 = p c }
                      (   λ(r : Type)
                        → λ ( arrowsOut
                            :   ∀(z : cpObject)
                              → cp.arrow { _1 = z, _2 = p c }
                              → cp.arrow { _1 = a, _2 = z }
                              → r
                            )
                        → arrowsOut b k fn
                      )
                    )
              }
            : ./../Functor/Type
              vObject
              cpObject
              dObject
              (base Type vObject v)
              cp
              d
              (Ran p f)
