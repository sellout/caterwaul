let cat = ./../Category/Cat/semigroupal

let Category = ./../Category/Kind cat

let vObject = Type

in    λ(v : ./../Category/Monoidal/Kind cat Type vObject)
    → λ(cObject : Kind)
    → λ(c : Category vObject cObject)
    → λ(dObject : Kind)
    → λ(d : Category vObject dObject)
    → λ(dCategory : ./../Category/Type dObject v d)
    → let object = cat.arrow cObject dObject
      
      let arrow = ./Type cObject dObject d
      
      in    { unit =
                λ(a : object) → λ(i : cObject) → dCategory.unit (a i)
            , product =
                  λ(a : { _1 : object, _2 : object })
                → λ ( p
                    :   ∀(r : Type)
                      → (   ∀(z : object)
                          → arrow { _1 = z, _2 = a._2 }
                          → arrow { _1 = a._1, _2 = z }
                          → r
                        )
                      → r
                    )
                → λ(i : cObject)
                → dCategory.product
                  { _1 = a._1 i, _2 = a._2 i }
                  (   λ(r : Type)
                    → λ ( arrowsOut
                        :   ∀(z : dObject)
                          → d.arrow { _1 = z, _2 = a._2 i }
                          → d.arrow { _1 = a._1 i, _2 = z }
                          → r
                        )
                    → p
                      r
                      (   λ(z : object)
                        → λ(f : arrow { _1 = z, _2 = a._2 })
                        → λ(g : arrow { _1 = a._1, _2 = z })
                        → arrowsOut (z i) (f i) (g i)
                      )
                  )
            }
          : ./../Category/Type
            object
            v
            ( ./../Category/Functor/category
              cObject
              dObject
              (./../Category/Monoidal/extractCategory cat Type vObject v)
              c
              d
            )
