let kArrow =
      ./../../Function/Kind sha256:0b6372c593badac5fe880699fd1cf19dfba07c62fab935a8bad96915866f466e

let kProduct =
      ./../../Tuple/Kind sha256:03e1c52890f0cda1a2181cd3eb045d5344fc7c907fcbf836736c0e3222c5ffd8

in    λ ( object
        : Kind
        )
    → λ(arrow : kArrow (kProduct object object) Type)
    → λ ( category
        : ./../Type sha256:56874495587bd8bf58b702382562e4aed91603b64a2d6893413c72d5bf265596
          object
          arrow
        )
    → let opArrow =
            ./Type sha256:ed18079f3fe5ecb275074781d4f90158ec3fdbc9e48c40e77c1f9af77a8dd387
            object
            arrow
      
      in    { identity =
                category.identity
            , op =
                  λ(a : { _1 : object, _2 : object })
                → λ ( p
                    :   ∀(r : Type)
                      → (   ∀(z : object)
                          → opArrow { _1 = z, _2 = a._2 }
                          → opArrow { _1 = a._1, _2 = z }
                          → r
                        )
                      → r
                    )
                → category.op
                  { _1 = a._2, _2 = a._1 }
                  (   λ(r : Type)
                    → λ ( arrowsOut
                        :   ∀(z : object)
                          → arrow { _1 = z, _2 = a._1 }
                          → arrow { _1 = a._2, _2 = z }
                          → r
                        )
                    → p
                      r
                      (   λ(z : object)
                        → λ(f : opArrow { _1 = z, _2 = a._2 })
                        → λ(g : opArrow { _1 = a._1, _2 = z })
                        → arrowsOut z g f
                      )
                  )
            }
          : ./../Type sha256:56874495587bd8bf58b702382562e4aed91603b64a2d6893413c72d5bf265596
            object
            opArrow
