let cat =
      ./../../Category/Cat/semigroupal sha256:de5b05c9640ea3ca0ee586c7102a982db79d3c0f0c92230cb2df19c7b986b513

let Category =
      ./../../Category/Kind sha256:c73426d12cddcdfc44b6e687fc897f7ef91fcda0c4da116a91acdca4d15c68a2
      cat

let Functor =
      ./../../Functor/Type sha256:bebd999f32d6a3fd9bd7151ca5f0942ed6aeded30809c4a7e6c91382bed2dbcb

in    λ ( vObject
        : Kind
        )
    → λ(v : Category Type vObject)
    → λ ( vCategory
        : ./../../Category/Type sha256:3983e08568dd06043567236a0b0ceb489d34346ea7cc29bd9c4d98bd567e03b9
          vObject
          ( ./../../Category/Monoidal/Cartesian/extractMonoidal sha256:8112e73d82fd63e4e8bb5628a8cea43f92518a02e56ad351c7f9558546e7f81a
            cat
            Type
            Type
            ./../../Category/Set/monoidal/cartesian sha256:3a79a17d1c39a3df58efcc62fd96a2da0147bb29b524f57aa7c35942f653f164
          )
          v
        )
    → λ(cObject : Kind)
    → λ(c : Category vObject cObject)
    → λ(dObject : Kind)
    → λ(d : Category vObject dObject)
    → λ(eObject : Kind)
    → λ(e : Category vObject eObject)
    → λ(f : cat.arrow dObject eObject)
    → λ(fFunctor : Functor vObject dObject eObject v d e f)
    → λ(g : cat.arrow cObject dObject)
    → λ(gFunctor : Functor vObject cObject dObject v c d g)
    →   { map =
              λ(a : cObject)
            → λ(b : cObject)
            → vCategory.product
              { _1 =
                  c.arrow { _1 = a, _2 = b }
              , _2 =
                  e.arrow { _1 = f (g a), _2 = f (g b) }
              }
              (   λ(r : Type)
                → λ ( arrowsOut
                    :   ∀(z : vObject)
                      → v.arrow
                        { _1 = z, _2 = e.arrow { _1 = f (g a), _2 = f (g b) } }
                      → v.arrow { _1 = c.arrow { _1 = a, _2 = b }, _2 = z }
                      → r
                    )
                → arrowsOut
                  (d.arrow { _1 = g a, _2 = g b })
                  (fFunctor.map (g a) (g b))
                  (gFunctor.map a b)
              )
        }
      : Functor
        vObject
        cObject
        eObject
        v
        c
        e
        ( ./../Type sha256:e830545569ce8ba04d3124a7ca93994fc93e18c0372616f45910253f0a00907d
          cObject
          dObject
          eObject
          { _1 = f, _2 = g }
        )
