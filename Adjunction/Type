let cat =
      ./../Category/Cat/semigroupal sha256:de5b05c9640ea3ca0ee586c7102a982db79d3c0f0c92230cb2df19c7b986b513

let Category =
      ./../Category/Kind sha256:c73426d12cddcdfc44b6e687fc897f7ef91fcda0c4da116a91acdca4d15c68a2
      cat

let vObject = Type

in    λ(cObject : Kind)
    → λ(dObject : Kind)
    → λ(c : Category vObject cObject)
    → λ(d : Category vObject dObject)
    → λ(f : cat.arrow dObject cObject)
    → λ(g : cat.arrow cObject dObject)
    → { unit :
          ∀(a : dObject) → d.arrow { _1 = a, _2 = g (f a) }
      , counit :
          ∀(a : cObject) → c.arrow { _1 = f (g a), _2 = a }
      , leftAdjunct :
            ∀(a : dObject)
          → ∀(b : cObject)
          → c.arrow { _1 = f a, _2 = b }
          → d.arrow { _1 = a, _2 = g b }
      , rightAdjunct :
            ∀(a : dObject)
          → ∀(b : cObject)
          → d.arrow { _1 = a, _2 = g b }
          → c.arrow { _1 = f a, _2 = b }
      }
