let kCat =
      ./../Category/Cat/semigroupal sha256:de5b05c9640ea3ca0ee586c7102a982db79d3c0f0c92230cb2df19c7b986b513

let vObject = Type

let v =
      ./../Category/Monoidal/Cartesian/extractMonoidal sha256:8112e73d82fd63e4e8bb5628a8cea43f92518a02e56ad351c7f9558546e7f81a
      kCat
      Type
      vObject
      ./../Category/Set/monoidal/cartesian sha256:3a79a17d1c39a3df58efcc62fd96a2da0147bb29b524f57aa7c35942f653f164

let base =
      ./../Category/Monoidal/extractCategory sha256:c9807a603a6bd9f0ef13debb0f11e63d6f7133870fef175f27d08dd81c2ff695
      kCat
      vObject

in    λ ( cObject
        : Kind
        )
    → λ ( c
        : ./../Category/Monoidal/Kind sha256:8e77a076f4c2f6e1d0ad07cfec37dfd04782e1a0a7e98a72a305233f69c494ca
          kCat
          vObject
          cObject
        )
    → λ ( semigroupoid
        : ./../Semigroupoid/Type sha256:40a8e489fddee04bb4fda6df908b24c2143a7f75b4c5e25eec9fd34e7947bbb8
          cObject
          v
          (base cObject c)
          c.arrow
        )
    → let Day =
            ./Type sha256:f2b5b6fb9d2fead2e46f0fe8362e08133d5f552c932d9ca03126f256d24b8520
            cObject
            v
            c
      
      in    λ ( f
              : kCat.arrow cObject vObject
              )
          → λ(g : kCat.arrow cObject vObject)
          →   { map =
                    λ ( a
                      : cObject
                      )
                  → λ(b : cObject)
                  → λ(fn : c.arrow { _1 = a, _2 = b })
                  → λ(day : Day { _1 = f, _2 = g } a)
                  → day
                    (Day { _1 = f, _2 = g } b)
                    (   λ ( d
                          : { _1 : cObject, _2 : cObject }
                          )
                      → λ(fn2 : c.arrow { _1 = c.product d, _2 = a })
                      → λ(e : v.product { _1 = f d._1, _2 = g d._2 })
                      → λ(r0 : vObject)
                      → λ ( day0
                          : ./convolution sha256:ed0886d09259f7be70d35f7735d511b586438cc026f26fcf254b617f9a59610f
                            cObject
                            v
                            c
                            f
                            g
                            b
                            r0
                          )
                      → day0
                        d
                        ( semigroupoid.product
                          { _1 = c.product d, _2 = b }
                          (   λ(r : Type)
                            → λ ( arrowsOut
                                :   ∀(z : cObject)
                                  → c.arrow { _1 = z, _2 = b }
                                  → c.arrow { _1 = c.product d, _2 = z }
                                  → r
                                )
                            → arrowsOut a fn fn2
                          )
                        )
                        e
                    )
              }
            : ./../Functor/Type sha256:bebd999f32d6a3fd9bd7151ca5f0942ed6aeded30809c4a7e6c91382bed2dbcb
              vObject
              cObject
              vObject
              (base vObject v)
              (base cObject c)
              (base vObject v)
              (Day { _1 = f, _2 = g })
