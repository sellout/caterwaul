let cat =
      ./../Category/Cat/semigroupal sha256:de5b05c9640ea3ca0ee586c7102a982db79d3c0f0c92230cb2df19c7b986b513

let base =
      ./../Category/Monoidal/Cartesian/extractCategory sha256:5d481c2c40201add9d14edc5a5ff6471a11fa9c58fea348279927339cfd60456
      cat

let vObject = Type

let v =
      base
      Type
      vObject
      ./../Category/Set/monoidal/cartesian sha256:3a79a17d1c39a3df58efcc62fd96a2da0147bb29b524f57aa7c35942f653f164

in    λ ( cObject
        : Kind
        )
    → λ(cpObject : Kind)
    → λ ( cp
        : ./../Category/Kind sha256:c73426d12cddcdfc44b6e687fc897f7ef91fcda0c4da116a91acdca4d15c68a2
          cat
          vObject
          cpObject
        )
    → let dObject =
            Type
      
      let d =
            base
            vObject
            dObject
            ./../Category/Set/monoidal/cartesian sha256:3a79a17d1c39a3df58efcc62fd96a2da0147bb29b524f57aa7c35942f653f164
      
      let dSemigroupoid =
            ./../Category/Set/semigroupoid sha256:fd98b398b43b5fe73e6a1a17a26c8a9a6cf5d9c712bfcbb29b7e1d42d12a4d10
      
      in    λ ( h
              : cat.arrow cpObject dObject
              )
          → λ ( functor
              : ./../Functor/Type sha256:bebd999f32d6a3fd9bd7151ca5f0942ed6aeded30809c4a7e6c91382bed2dbcb
                vObject
                cpObject
                dObject
                v
                cp
                d
                h
              )
          → λ(f : cat.arrow cObject cpObject)
          → λ(g : cat.arrow cObject dObject)
          → λ(duplicate : ∀(b : cObject) → d.arrow { _1 = g b, _2 = h (f b) })
          → λ(a : cpObject)
          → λ ( lan
              : ./Type sha256:2edbc6596b56524c06bd7d0f89e30a4a273b5a8a2e53c65c09f2e2ae340891a4
                cObject
                cpObject
                cp
                f
                g
                a
              )
          → lan
            (h a)
            (   λ(b : cObject)
              → λ(k : cp.arrow { _1 = f b, _2 = a })
              → dSemigroupoid.product
                { _1 = g b, _2 = h a }
                (   λ(r : Type)
                  → λ ( arrowsOut
                      :   ∀(z : dObject)
                        → d.arrow { _1 = z, _2 = h a }
                        → d.arrow { _1 = g b, _2 = z }
                        → r
                      )
                  → arrowsOut (h (f b)) (functor.map (f b) a k) (duplicate b)
                )
            )
