{- If there is a natural transformation from `n` to `m`, then a m-Star functor
   can be made into an n-Star functor.
-}
let kCat =
      ./../../Category/Cat/semigroupal sha256:de5b05c9640ea3ca0ee586c7102a982db79d3c0f0c92230cb2df19c7b986b513

let vObject = Type

let v =
      ./../../Category/Monoidal/Cartesian/extractMonoidal sha256:25afdf2818357f9c4b9dac6652b82e730472c389a634f8417a6014d39c7cddc0
      kCat
      Type
      vObject
      ./../../Category/Set/monoidal/cartesian sha256:48e8bea552638624a62335eab92acd1e0a04c28e8a0d8fe1c8974565065a9576

let Set =
      ./../../Category/Monoidal/Cartesian/extractCategory sha256:fe359f472fbc38300f29f9f50522e2e23d0bb16034bc98c93265a1f5aeb01349
      kCat
      vObject
      Type
      ./../../Category/Set/monoidal/cartesian sha256:48e8bea552638624a62335eab92acd1e0a04c28e8a0d8fe1c8974565065a9576

in    λ ( object
        : Kind
        )
    → λ ( cat
        : ./../../Category/Kind sha256:c73426d12cddcdfc44b6e687fc897f7ef91fcda0c4da116a91acdca4d15c68a2
          kCat
          vObject
          object
        )
    → λ ( category
        : ./../../Category/Type sha256:5792bd3a482ad0d318aecda464bd952a6561dc1119bdc1f5db574cfc3b66e095
          object
          v
          cat
        )
    → let vBase =
            ./../../Category/Monoidal/extractCategory sha256:936b393338b8dcf60af6c25ecea2b0b02d83e18acfed60ff736754fe0c1d7b3d
            kCat
            Type
            vObject
            v
      
      let endoObject = kCat.arrow object object
      
      let endo =
            ./../../Category/Monoidal/extractCategory sha256:936b393338b8dcf60af6c25ecea2b0b02d83e18acfed60ff736754fe0c1d7b3d
            kCat
            vObject
            endoObject
            ( ./../../Category/Endofunctor/monoidal/monadic sha256:6c2ff9a2294a8955a06b7ccad72ba28e11587ecb0882b7ecd1702b56c59d2f38
              object
              vBase
              cat
            )
      
      in    λ ( f
              : endoObject
              )
          →   { map =
                    λ ( m
                      : endoObject
                      )
                  → λ(n : endoObject)
                  → λ(natural : endo.arrow { _1 = n, _2 = m })
                  → λ ( star
                      : ./Type sha256:478e56de78e7bcfddaa56adfb8e10e97007303aee206e5a64080fe7210bfb539
                        vObject
                        object
                        vBase
                        cat
                        m
                        f
                      )
                  → { map =
                          λ(a : object)
                        → λ(b : object)
                        → λ(fn : cat.arrow { _1 = a, _2 = n b })
                        → star.map
                          a
                          b
                          ( category.op
                            { _1 = a, _2 = m b }
                            (   λ(r : Type)
                              → λ ( arrowsOut
                                  :   ∀(z : object)
                                    → cat.arrow { _1 = z, _2 = m b }
                                    → cat.arrow { _1 = a, _2 = z }
                                    → r
                                  )
                              → arrowsOut (n b) (natural b) fn
                            )
                          )
                    }
              }
            : ./../../Functor/Contravariant sha256:5cac00b722fed910bc5b8637f00ec1361decf3c8936a4336631a4cf3ba1d20e6
              vObject
              endoObject
              Type
              vBase
              endo
              Set
              (   λ ( a
                    : endoObject
                    )
                → ./Type sha256:478e56de78e7bcfddaa56adfb8e10e97007303aee206e5a64080fe7210bfb539
                  vObject
                  object
                  vBase
                  cat
                  a
                  f
              )
