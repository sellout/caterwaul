let cat =
      ./../Category/Cat/semigroupal sha256:de5b05c9640ea3ca0ee586c7102a982db79d3c0f0c92230cb2df19c7b986b513

let vObject = Type

let v =
      ./../Category/Monoidal/Cartesian/extractMonoidal sha256:25afdf2818357f9c4b9dac6652b82e730472c389a634f8417a6014d39c7cddc0
      cat
      Type
      vObject
      ./../Category/Set/monoidal/cartesian sha256:48e8bea552638624a62335eab92acd1e0a04c28e8a0d8fe1c8974565065a9576

let base =
      ./../Category/Monoidal/extractCategory sha256:936b393338b8dcf60af6c25ecea2b0b02d83e18acfed60ff736754fe0c1d7b3d
      cat
      Type
      vObject
      v

in    λ ( semigroupoid
        : ./../Semigroupoid/Type sha256:c43adcdb5e9cb39774277d7dcbeb77a8c85a6d64458b129d1a92bac3711f132d
          vObject
          v
          base
          v.arrow
        )
    → let Comonad =
            ./../Comonad/Type sha256:d13db9e0b984aa3ad4e658ae2574973a4a477d0f8ca3b88c3b5a79d0799536c8
            vObject
            base
      
      let Day =
            ./Type sha256:75bb9592fce61b6be1565b8b7589763f51b71a9dab310fd5b9c72dbce176a67f
            vObject
            v
            v
      
      let Day/convolution =
            ./../Day/convolution sha256:ca965ddd64a13ff9661a3e175fa344344e188283b59f6620afb6ffb36fe95ba5
            vObject
            v
            v
      
      in    λ(f : cat.arrow vObject vObject)
          → λ(fComonad : Comonad f)
          → λ(g : cat.arrow vObject vObject)
          → λ(gComonad : Comonad g)
          →   { identity =
                    λ(a : vObject)
                  → λ(day : Day { _1 = f, _2 = g } a)
                  → day
                    a
                    (   λ(b : { _1 : vObject, _2 : vObject })
                      → λ(fn : { _1 : b._1, _2 : b._2 } → a)
                      → λ(d : { _1 : f b._1, _2 : g b._2 })
                      → fn
                        { _1 =
                            fComonad.identity b._1 d._1
                        , _2 =
                            gComonad.identity b._2 d._2
                        }
                    )
              , op =
                    λ(a : vObject)
                  → λ(day : Day { _1 = f, _2 = g } a)
                  → day
                    (Day { _1 = f, _2 = g } (Day { _1 = f, _2 = g } a))
                    (   λ(b : { _1 : vObject, _2 : vObject })
                      → λ(fn : { _1 : b._1, _2 : b._2 } → a)
                      → λ(d : { _1 : f b._1, _2 : g b._2 })
                      → λ(r0 : vObject)
                      → λ ( day0
                          : Day/convolution f g (Day { _1 = f, _2 = g } a) r0
                          )
                      → day0
                        { _1 = f b._1, _2 = g b._2 }
                        (   λ(p : { _1 : f b._1, _2 : g b._2 })
                          → λ(r1 : vObject)
                          → λ(day1 : Day/convolution f g a r1)
                          → day1 b fn p
                        )
                        { _1 =
                            fComonad.op b._1 d._1
                        , _2 =
                            gComonad.op b._2 d._2
                        }
                    )
              }
            : Comonad (Day { _1 = f, _2 = g })
