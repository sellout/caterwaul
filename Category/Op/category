let kCat =
      ./../Cat/semigroupal sha256:de5b05c9640ea3ca0ee586c7102a982db79d3c0f0c92230cb2df19c7b986b513

let vObject = Type

in    λ ( object
        : Kind
        )
    → λ ( v
        : ./../Monoidal/Kind sha256:96b5eca83adb85b7bc744975b530210da2aec17c9c70b45d3b241ccfdb47e772
          kCat
          Type
          vObject
        )
    → λ ( cat
        : ./../Kind sha256:c73426d12cddcdfc44b6e687fc897f7ef91fcda0c4da116a91acdca4d15c68a2
          kCat
          vObject
          object
        )
    → λ ( category
        : ./../Type sha256:a3968397f7c9a309d7c4eed545517a4e6f451eff463aaaf274fd1c7d37cb3004
          object
          v
          cat
        )
    → let Op =
            ./Kind sha256:e098dd6a242e1c53f07ac907a04e6196a2d8055fe8e95650872a5b89ff60c7b4
            vObject
            object
            cat
      
      in    { identity =
                category.identity
            , product =
                  λ(a : { _1 : object, _2 : object })
                → λ ( p
                    :   ∀(r : Type)
                      → (   ∀(z : object)
                          → Op.arrow { _1 = z, _2 = a._2 }
                          → Op.arrow { _1 = a._1, _2 = z }
                          → r
                        )
                      → r
                    )
                → category.product
                  { _1 = a._2, _2 = a._1 }
                  (   λ(r : Type)
                    → λ ( arrowsOut
                        :   ∀(z : object)
                          → cat.arrow { _1 = z, _2 = a._1 }
                          → cat.arrow { _1 = a._2, _2 = z }
                          → r
                        )
                    → p
                      r
                      (   λ(z : object)
                        → λ(f : Op.arrow { _1 = z, _2 = a._2 })
                        → λ(g : Op.arrow { _1 = a._1, _2 = z })
                        → arrowsOut z g f
                      )
                  )
            }
          : ./../Type sha256:a3968397f7c9a309d7c4eed545517a4e6f451eff463aaaf274fd1c7d37cb3004
            object
            v
            Op
