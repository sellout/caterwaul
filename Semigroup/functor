let kArrow = ./../Function/Kind

let kProduct = ./../Tuple/Kind

in    λ(object : Kind)
    → λ(cat : ./../Category/Semigroupal/Kind kArrow kProduct object)
    → λ(semigroupoid : ./../Semigroupoid/Type object cat.arrow)
    → λ(bifunctor : ./../Functor/Pair/Type object cat.arrow cat.op)
    → let Semigroup = ./Type object cat
      
      let Dagger = ./../Category/Dagger/Type object cat.arrow
      
      in    { map =
                  λ(a : object)
                → λ(b : object)
                → λ(fn : Dagger { _1 = a, _2 = b })
                → λ(fa : Semigroup a)
                → { op =
                      semigroupoid.op
                      { _1 = cat.op { _1 = b, _2 = b }, _2 = b }
                      (   λ(r : Type)
                        → λ ( arrowsOut
                            :   ∀(z : object)
                              → cat.arrow { _1 = z, _2 = b }
                              → cat.arrow
                                { _1 = cat.op { _1 = b, _2 = b }, _2 = z }
                              → r
                            )
                        → arrowsOut
                          a
                          fn.to
                          ( semigroupoid.op
                            { _1 = cat.op { _1 = b, _2 = b }, _2 = a }
                            (   λ(r : Type)
                              → λ ( arrowsOut
                                  :   ∀(z : object)
                                    → cat.arrow { _1 = z, _2 = a }
                                    → cat.arrow
                                      { _1 = cat.op { _1 = b, _2 = b }, _2 = z }
                                    → r
                                  )
                              → arrowsOut
                                (cat.op { _1 = a, _2 = a })
                                fa.op
                                ( bifunctor.map
                                  { _1 = b, _2 = b }
                                  { _1 = a, _2 = a }
                                  { _1 = fn.from, _2 = fn.from }
                                )
                            )
                          )
                      )
                  }
            }
          : ./../Functor/Type object Type Dagger ./../Function/Type Semigroup
