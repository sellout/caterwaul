let kArrow =
      ./../Function/Kind sha256:0b6372c593badac5fe880699fd1cf19dfba07c62fab935a8bad96915866f466e

let kProduct =
      ./../Tuple/Kind sha256:03e1c52890f0cda1a2181cd3eb045d5344fc7c907fcbf836736c0e3222c5ffd8

in    λ ( cObject
        : Kind
        )
    → λ(cpObject : Kind)
    → λ(cpArrow : kArrow (kProduct cpObject cpObject) Type)
    → λ ( cpSemigroupoid
        : ./../Semigroupoid/Type sha256:7ea3ed050672efbe97058fdf8cee2c448261528fb9df88e77792684384125059
          cpObject
          cpArrow
        )
    → let dObject =
            Type
      
      let dArrow =
            ./../Function/Type sha256:44d58123a7ba6bd4f59a8f0fe8363ca585db3061ae3f369d3a26607a127f7927
      
      let Lan =
            ./Type sha256:0a24ae0e2725039bedcdd6985fc300164a47ad8eeef4cbbed689d0b13a05fe1f
            cObject
            cpObject
            cpArrow
      
      in    λ ( f
              : kArrow cObject cpObject
              )
          → λ(g : kArrow cObject dObject)
          →   { map =
                    λ(a : cpObject)
                  → λ(b : cpObject)
                  → λ(h : cpArrow { _1 = a, _2 = b })
                  → λ(lan : Lan f g a)
                  → λ(r : Type)
                  → λ ( k
                      :   ∀(c : cObject)
                        → cpArrow { _1 = f c, _2 = b }
                        → dArrow { _1 = g c, _2 = r }
                      )
                  → lan
                    r
                    (   λ(c : cObject)
                      → λ(x : cpArrow { _1 = f c, _2 = a })
                      → k
                        c
                        ( cpSemigroupoid.op
                          { _1 = f c, _2 = b }
                          (   λ(r : Type)
                            → λ ( arrowsOut
                                :   ∀(z : cpObject)
                                  → cpArrow { _1 = z, _2 = b }
                                  → cpArrow { _1 = f c, _2 = z }
                                  → r
                                )
                            → arrowsOut a h x
                          )
                        )
                    )
              }
            : ./../Functor/Type sha256:fe94261bd581198ae1523ffbe7037159ad8327199b42ee3f97d16a7e1fa1dc79
              cpObject
              dObject
              cpArrow
              dArrow
              (Lan f g)
