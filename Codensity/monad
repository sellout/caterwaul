let kCat =
      ./../Category/Cat/semigroupal sha256:de5b05c9640ea3ca0ee586c7102a982db79d3c0f0c92230cb2df19c7b986b513

let vObject = Type

let v =
      ./../Category/Monoidal/Cartesian/extractMonoidal sha256:25afdf2818357f9c4b9dac6652b82e730472c389a634f8417a6014d39c7cddc0
      kCat
      Type
      vObject
      ./../Category/Set/monoidal/cartesian sha256:48e8bea552638624a62335eab92acd1e0a04c28e8a0d8fe1c8974565065a9576

let object = Type

let cat =
      ./../Category/Monoidal/Cartesian/extractCategory sha256:fe359f472fbc38300f29f9f50522e2e23d0bb16034bc98c93265a1f5aeb01349
      kCat
      vObject
      object
      ./../Category/Set/monoidal/cartesian sha256:48e8bea552638624a62335eab92acd1e0a04c28e8a0d8fe1c8974565065a9576

in    λ ( category
        : ./../Category/Type sha256:5792bd3a482ad0d318aecda464bd952a6561dc1119bdc1f5db574cfc3b66e095
          object
          v
          cat
        )
    → let Codensity =
            ./Type sha256:b0945f99031bc91ed9a0bc7c25b48ef903bf0c3746ac6fcaaeb04fefa0b652e1
            object
            cat
      
      in    λ ( m
              : kCat.arrow object object
              )
          →   { identity =
                    λ(a : object)
                  → λ(x : a)
                  → λ(b : object)
                  → λ(k : cat.arrow { _1 = a, _2 = m b })
                  → k x
              , op =
                    λ(a : object)
                  → λ(codensity : Codensity m (Codensity m a))
                  → λ(c : object)
                  → λ(l : cat.arrow { _1 = a, _2 = m c })
                  → codensity
                    c
                    (   λ(x : Codensity m a)
                      → category.identity (Codensity m a) x c l
                    )
              }
            : ./../Monad/Type sha256:d47c5141940d87e0a1ed370c52c27ddb1be4330b8c6684c90247f1f72eb77876
              object
              cat
              (Codensity m)
