{- If there is a natural transformation from `n` to `m`, then a m-Star functor
   can be made into an n-Star functor.
-}
let kCat =
      ./../../Category/Cat/semigroupal sha256:de5b05c9640ea3ca0ee586c7102a982db79d3c0f0c92230cb2df19c7b986b513

let vObject = Type

let v =
      ./../../Category/Monoidal/Cartesian/extractMonoidal sha256:0fcc63fc837459ca2866d31c5b11400b56fcb8f7d1846b774e98b47995c1879a
      kCat
      Type
      vObject
      ./../../Category/Set/monoidal/cartesian sha256:b6efbdc0effbca1bc7427c980f2579012808494a8195fdf9fb52aea37ae91116

let Set =
      ./../../Category/Monoidal/Cartesian/extractCategory sha256:fef093587bdc97a914e4e30469579c22f8fb63653ea5f511606e0e8109662a4b
      kCat
      vObject
      Type
      ./../../Category/Set/monoidal/cartesian sha256:b6efbdc0effbca1bc7427c980f2579012808494a8195fdf9fb52aea37ae91116

in    λ ( object
        : Kind
        )
    → λ ( cat
        : ./../../Category/Kind sha256:c73426d12cddcdfc44b6e687fc897f7ef91fcda0c4da116a91acdca4d15c68a2
          kCat
          vObject
          object
        )
    → λ ( category
        : ./../../Category/Type sha256:a3968397f7c9a309d7c4eed545517a4e6f451eff463aaaf274fd1c7d37cb3004
          object
          v
          cat
        )
    → let vBase =
            ./../../Category/Monoidal/extractCategory sha256:be63455106b8fe90ee7366a02f05b29c1aa42e63243bb9994d55011052eab6ec
            kCat
            Type
            vObject
            v
      
      let endoObject = kCat.arrow object object
      
      let endo =
            ./../../Category/Monoidal/extractCategory sha256:be63455106b8fe90ee7366a02f05b29c1aa42e63243bb9994d55011052eab6ec
            kCat
            vObject
            endoObject
            ( ./../../Category/Endofunctor/monoidal/monadic sha256:bc90904e831033ee75dfbaca1c8bbf79c20633bae1c27ab297460fb43bddbaaa
              object
              vBase
              cat
            )
      
      in    λ ( f
              : endoObject
              )
          →   { map =
                    λ ( m
                      : endoObject
                      )
                  → λ(n : endoObject)
                  → λ(natural : endo.arrow { _1 = n, _2 = m })
                  → λ ( star
                      : ./Type sha256:478e56de78e7bcfddaa56adfb8e10e97007303aee206e5a64080fe7210bfb539
                        vObject
                        object
                        vBase
                        cat
                        m
                        f
                      )
                  → { map =
                          λ(a : object)
                        → λ(b : object)
                        → λ(fn : cat.arrow { _1 = a, _2 = n b })
                        → star.map
                          a
                          b
                          ( category.product
                            { _1 = a, _2 = m b }
                            (   λ(r : Type)
                              → λ ( arrowsOut
                                  :   ∀(z : object)
                                    → cat.arrow { _1 = z, _2 = m b }
                                    → cat.arrow { _1 = a, _2 = z }
                                    → r
                                  )
                              → arrowsOut (n b) (natural b) fn
                            )
                          )
                    }
              }
            : ./../../Functor/Contravariant sha256:5cac00b722fed910bc5b8637f00ec1361decf3c8936a4336631a4cf3ba1d20e6
              vObject
              endoObject
              Type
              vBase
              endo
              Set
              (   λ ( a
                    : endoObject
                    )
                → ./Type sha256:478e56de78e7bcfddaa56adfb8e10e97007303aee206e5a64080fe7210bfb539
                  vObject
                  object
                  vBase
                  cat
                  a
                  f
              )
