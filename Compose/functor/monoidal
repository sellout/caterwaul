{- NB: Since this is currently forced to be in *Set*, it’s quite possible that
       some of the uses of _C_ and _D_ are wrong, since the type system can’t
       distinguish them. So take them with a grain of salt if you’re editing
       this file.
-}
let kArrow =
      ./../../Function/Kind sha256:0b6372c593badac5fe880699fd1cf19dfba07c62fab935a8bad96915866f466e

let kProduct =
      ./../../Tuple/Kind sha256:03e1c52890f0cda1a2181cd3eb045d5344fc7c907fcbf836736c0e3222c5ffd8

let vObject = Type

let MonoidalFunctor =
      ./../../Functor/Monoidal/Type sha256:3fd0a2398dd1d3c0cb029ce034f09d4c1b6f51e622139f06ad1086ceea926567

in    λ ( cObject
        : Kind
        )
    → λ ( v
        : ./../../Category/Kind sha256:41ea86fd34226d0dc3c87cc12f9ce042e3b54e7eb77b451cdbfb085ba3f66916
          kArrow
          kProduct
          Type
          vObject
        )
    → λ ( vCategory
        : ./../../Category/Type sha256:b5245b1f92f65f1b72bbfac6c53ebc19dc3c778b775b19ea4ad73fa6d2569dcb
          vObject
          ./../../Category/Set/monoidal/cartesian sha256:b017581ee66eec1995d52c80d527598dd5614254bf7217768c035c3a5e2103bd
          v
        )
    → λ ( c
        : ./../../Category/Monoidal/Kind sha256:f05d8665b46c686b6b930ad18aad6e1daa8c7a9c104d7bbeec947389d4d83e48
          kArrow
          kProduct
          vObject
          cObject
        )
    → let dObject =
            Type
      
      let d =
            ./../../Category/Set/monoidal/cartesian sha256:b017581ee66eec1995d52c80d527598dd5614254bf7217768c035c3a5e2103bd
      
      let eObject = Type
      
      let e =
            ./../../Category/Set/monoidal/cartesian sha256:b017581ee66eec1995d52c80d527598dd5614254bf7217768c035c3a5e2103bd
      
      let Compose =
            ./../Type sha256:e18cb4b2004c6cf2789b492be4e1b912e3299eca94c229dc5913a11d55bcd72c
            cObject
            dObject
            eObject
      
      let Day =
            ./../../Day/Type sha256:08b059e69330b8894b4a1830425eea70c2e4a0366c97e99cad8c073283bab16d
            cObject
            e
            c
      
      let Day/convolution =
            ./../../Day/convolution sha256:e9cc6113ab1965750eabb0f553e45e3818548a78755967d465d6f3082e674913
      
      in    λ(f : kArrow dObject eObject)
          → λ(fMonoidal : MonoidalFunctor dObject v d e f)
          → λ(g : kArrow cObject dObject)
          → λ(gMonoidal : MonoidalFunctor cObject v c d g)
          →   { map =
                    λ(a : cObject)
                  → λ(b : cObject)
                  → vCategory.op
                    { _1 =
                        c.arrow { _1 = a, _2 = b }
                    , _2 =
                        e.arrow { _1 = f (g a), _2 = f (g b) }
                    }
                    (   λ(r : Type)
                      → λ ( arrowsOut
                          :   ∀(z : vObject)
                            → v.arrow
                              { _1 =
                                  z
                              , _2 =
                                  e.arrow { _1 = f (g a), _2 = f (g b) }
                              }
                            → v.arrow
                              { _1 = c.arrow { _1 = a, _2 = b }, _2 = z }
                            → r
                          )
                      → arrowsOut
                        (d.arrow { _1 = g a, _2 = g b })
                        (fMonoidal.map (g a) (g b))
                        (gMonoidal.map a b)
                    )
              , identity =
                    λ(a : cObject)
                  → λ(fn : c.arrow { _1 = c.identity, _2 = a })
                  → fMonoidal.identity
                    (g a)
                    (λ(_ : d.identity) → gMonoidal.identity a fn)
              , op =
                    λ(i : cObject)
                  → λ ( day
                      : Day
                        { _1 =
                            Compose { _1 = f, _2 = g }
                        , _2 =
                            Compose { _1 = f, _2 = g }
                        }
                        i
                      )
                  → day
                    (f (g i))
                    (   λ(b : { _1 : cObject, _2 : cObject })
                      → λ(fn : c.arrow { _1 = c.op b, _2 = i })
                      → λ(a : { _1 : f (g b._1), _2 : f (g b._2) })
                      → fMonoidal.op
                        (g i)
                        (   λ(fr : eObject)
                          → λ(fDay : Day/convolution dObject e d f f (g i) fr)
                          → fDay
                            { _1 = g b._1, _2 = g b._2 }
                            (   λ(p : { _1 : g b._1, _2 : g b._2 })
                              → gMonoidal.op
                                i
                                (   λ(gr : dObject)
                                  → λ ( gDay
                                      : Day/convolution cObject d c g g i gr
                                      )
                                  → gDay b fn p
                                )
                            )
                            a
                        )
                    )
              }
            : MonoidalFunctor cObject v c e (Compose { _1 = f, _2 = g })
