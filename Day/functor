let kCat =
      ./../Category/Cat/semigroupal sha256:de5b05c9640ea3ca0ee586c7102a982db79d3c0f0c92230cb2df19c7b986b513

let vObject = Type

let v =
      ./../Category/Monoidal/Cartesian/extractMonoidal sha256:0fcc63fc837459ca2866d31c5b11400b56fcb8f7d1846b774e98b47995c1879a
      kCat
      Type
      vObject
      ./../Category/Set/monoidal/cartesian sha256:b6efbdc0effbca1bc7427c980f2579012808494a8195fdf9fb52aea37ae91116

let base =
      ./../Category/Monoidal/extractCategory sha256:be63455106b8fe90ee7366a02f05b29c1aa42e63243bb9994d55011052eab6ec
      kCat
      vObject

in    λ ( cObject
        : Kind
        )
    → λ ( c
        : ./../Category/Monoidal/Kind sha256:96b5eca83adb85b7bc744975b530210da2aec17c9c70b45d3b241ccfdb47e772
          kCat
          vObject
          cObject
        )
    → λ ( semigroupoid
        : ./../Semigroupoid/Type sha256:b5492226c5be6bd719ab035c9286b707b273e6b191f3b8b572e51394688717c6
          cObject
          v
          (base cObject c)
          c.arrow
        )
    → let Day =
            ./Type sha256:ba33d670ee62e655ba9a4e6345ce1dca028a782f36a37bb8e0fcd3ae3881f9c7
            cObject
            v
            c
      
      in    λ ( f
              : kCat.arrow cObject vObject
              )
          → λ(g : kCat.arrow cObject vObject)
          →   { map =
                    λ ( a
                      : cObject
                      )
                  → λ(b : cObject)
                  → λ(fn : c.arrow { _1 = a, _2 = b })
                  → λ(day : Day { _1 = f, _2 = g } a)
                  → day
                    (Day { _1 = f, _2 = g } b)
                    (   λ ( d
                          : { _1 : cObject, _2 : cObject }
                          )
                      → λ(fn2 : c.arrow { _1 = c.product d, _2 = a })
                      → λ(e : v.product { _1 = f d._1, _2 = g d._2 })
                      → λ(r0 : vObject)
                      → λ ( day0
                          : ./convolution sha256:9c319003951ddc83300318bd31dee48bc16d2f48890593ed4363470ab6763426
                            cObject
                            v
                            c
                            f
                            g
                            b
                            r0
                          )
                      → day0
                        d
                        ( semigroupoid.product
                          { _1 = c.product d, _2 = b }
                          (   λ(r : Type)
                            → λ ( arrowsOut
                                :   ∀(z : cObject)
                                  → c.arrow { _1 = z, _2 = b }
                                  → c.arrow { _1 = c.product d, _2 = z }
                                  → r
                                )
                            → arrowsOut a fn fn2
                          )
                        )
                        e
                    )
              }
            : ./../Functor/Type sha256:bebd999f32d6a3fd9bd7151ca5f0942ed6aeded30809c4a7e6c91382bed2dbcb
              vObject
              cObject
              vObject
              (base vObject v)
              (base cObject c)
              (base vObject v)
              (Day { _1 = f, _2 = g })
