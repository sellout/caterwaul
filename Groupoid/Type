{- TODO: This would be `./../Group/Type … (./../Category/Monoidal/Profunctor
         object morphism)`, but `Category` isn’t defined via `Monoid`, so we
		 prefer compatibility with that.
-}
let kCat =
      ./../Category/Cat/semigroupal sha256:de5b05c9640ea3ca0ee586c7102a982db79d3c0f0c92230cb2df19c7b986b513

let vObject = Type

in    λ ( object
        : Kind
        )
    → λ ( v
        : ./../Category/Monoidal/Kind sha256:96b5eca83adb85b7bc744975b530210da2aec17c9c70b45d3b241ccfdb47e772
          kCat
          Type
          vObject
        )
    → λ ( cat
        : ./../Category/Kind sha256:c73426d12cddcdfc44b6e687fc897f7ef91fcda0c4da116a91acdca4d15c68a2
          kCat
          vObject
          object
        )
    → let profunctor =
            kCat.arrow (kCat.product object object) vObject
      
      in    λ ( morphism
              : profunctor
              )
          →   { inverse :
                  ∀(a : { _1 : object, _2 : object }) → morphism a → morphism a
              }
            ⩓ ./../Category/Type sha256:a3968397f7c9a309d7c4eed545517a4e6f451eff463aaaf274fd1c7d37cb3004
              object
              v
              cat
