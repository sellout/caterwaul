let cat =
      ./../Category/Cat/semigroupal sha256:de5b05c9640ea3ca0ee586c7102a982db79d3c0f0c92230cb2df19c7b986b513

let monoidalBase =
      ./../Category/Monoidal/Cartesian/extractMonoidal sha256:8112e73d82fd63e4e8bb5628a8cea43f92518a02e56ad351c7f9558546e7f81a
      cat

let base =
      ./../Category/Monoidal/extractCategory sha256:c9807a603a6bd9f0ef13debb0f11e63d6f7133870fef175f27d08dd81c2ff695
      cat

let vObject = Type

let v =
      monoidalBase
      Type
      vObject
      ./../Category/Set/monoidal/cartesian sha256:3a79a17d1c39a3df58efcc62fd96a2da0147bb29b524f57aa7c35942f653f164

in    λ ( cObject
        : Kind
        )
    → λ(cpObject : Kind)
    → λ ( cp
        : ./../Category/Kind sha256:c73426d12cddcdfc44b6e687fc897f7ef91fcda0c4da116a91acdca4d15c68a2
          cat
          vObject
          cpObject
        )
    → λ ( cpSemigroupoid
        : ./../Semigroupoid/Type sha256:40a8e489fddee04bb4fda6df908b24c2143a7f75b4c5e25eec9fd34e7947bbb8
          cpObject
          v
          cp
          cp.arrow
        )
    → let dObject =
            Type
      
      let d =
            base
            vObject
            dObject
            ( monoidalBase
              vObject
              dObject
              ./../Category/Set/monoidal/cartesian sha256:3a79a17d1c39a3df58efcc62fd96a2da0147bb29b524f57aa7c35942f653f164
            )
      
      let Ran =
            ./Type sha256:5047b1521693599d043a9c04270b2d4649481321743beae0ea4179975068d804
            cObject
            cpObject
            cp
      
      in    λ ( p
              : cat.arrow cObject cpObject
              )
          → λ(f : cat.arrow cObject dObject)
          →   { map =
                    λ(a : cpObject)
                  → λ(b : cpObject)
                  → λ(fn : cp.arrow { _1 = a, _2 = b })
                  → λ(ran : Ran p f a)
                  → λ(c : cObject)
                  → λ(k : cp.arrow { _1 = b, _2 = p c })
                  → ran
                    c
                    ( cpSemigroupoid.product
                      { _1 = a, _2 = p c }
                      (   λ(r : Type)
                        → λ ( arrowsOut
                            :   ∀(z : cpObject)
                              → cp.arrow { _1 = z, _2 = p c }
                              → cp.arrow { _1 = a, _2 = z }
                              → r
                            )
                        → arrowsOut b k fn
                      )
                    )
              }
            : ./../Functor/Type sha256:bebd999f32d6a3fd9bd7151ca5f0942ed6aeded30809c4a7e6c91382bed2dbcb
              vObject
              cpObject
              dObject
              (base Type vObject v)
              cp
              d
              (Ran p f)
