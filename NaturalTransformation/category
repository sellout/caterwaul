let kArrow =
      ./../Function/Kind sha256:0b6372c593badac5fe880699fd1cf19dfba07c62fab935a8bad96915866f466e

let kProduct =
      ./../Tuple/Kind sha256:03e1c52890f0cda1a2181cd3eb045d5344fc7c907fcbf836736c0e3222c5ffd8

in    λ ( cObject
        : Kind
        )
    → λ(cArrow : kArrow (kProduct cObject cObject) Type)
    → λ(dObject : Kind)
    → λ(dArrow : kArrow (kProduct dObject dObject) Type)
    → λ ( dCategory
        : ./../Category/Type sha256:56874495587bd8bf58b702382562e4aed91603b64a2d6893413c72d5bf265596
          dObject
          dArrow
        )
    → let object =
            kArrow cObject dObject
      
      let arrow =
            ./Type sha256:ac4ec9bf178715d3e80e93c72406abcaa99f0e70df64e30d76047afb6777ac95
            cObject
            dObject
            dArrow
      
      in    { identity =
                λ(a : object) → λ(i : cObject) → dCategory.identity (a i)
            , op =
                  λ(a : { _1 : object, _2 : object })
                → λ ( p
                    :   ∀(r : Type)
                      → (   ∀(z : object)
                          → arrow { _1 = z, _2 = a._2 }
                          → arrow { _1 = a._1, _2 = z }
                          → r
                        )
                      → r
                    )
                → λ(i : cObject)
                → dCategory.op
                  { _1 = a._1 i, _2 = a._2 i }
                  (   λ(r : Type)
                    → λ ( arrowsOut
                        :   ∀(z : dObject)
                          → dArrow { _1 = z, _2 = a._2 i }
                          → dArrow { _1 = a._1 i, _2 = z }
                          → r
                        )
                    → p
                      r
                      (   λ(z : object)
                        → λ(f : arrow { _1 = z, _2 = a._2 })
                        → λ(g : arrow { _1 = a._1, _2 = z })
                        → arrowsOut (z i) (f i) (g i)
                      )
                  )
            }
          : ./../Category/Type sha256:56874495587bd8bf58b702382562e4aed91603b64a2d6893413c72d5bf265596
            object
            arrow
