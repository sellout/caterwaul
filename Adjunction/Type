let kArrow =
      ./../Function/Kind sha256:0b6372c593badac5fe880699fd1cf19dfba07c62fab935a8bad96915866f466e

let kProduct =
      ./../Tuple/Kind sha256:03e1c52890f0cda1a2181cd3eb045d5344fc7c907fcbf836736c0e3222c5ffd8

in    λ(cObject : Kind)
    → λ(dObject : Kind)
    → λ(cArrow : kArrow (kProduct cObject cObject) Type)
    → λ(dArrow : kArrow (kProduct dObject dObject) Type)
    → λ(f : kArrow dObject cObject)
    → λ(g : kArrow cObject dObject)
    → { unit :
          ∀(a : dObject) → dArrow { _1 = a, _2 = g (f a) }
      , counit :
          ∀(a : cObject) → cArrow { _1 = f (g a), _2 = a }
      , leftAdjunct :
            ∀(a : dObject)
          → ∀(b : cObject)
          → cArrow { _1 = f a, _2 = b }
          → dArrow { _1 = a, _2 = g b }
      , rightAdjunct :
            ∀(a : dObject)
          → ∀(b : cObject)
          → dArrow { _1 = a, _2 = g b }
          → cArrow { _1 = f a, _2 = b }
      }
