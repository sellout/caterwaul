let kArrow = ./../Function/Kind

let kProduct = ./../Tuple/Kind

let vObject = Type

in    λ(v : ./../Category/Monoidal/Kind kArrow kProduct Type vObject)
    → λ(cObject : Kind)
    → λ(c : ./../Category/Kind kArrow kProduct vObject cObject)
    → λ(dObject : Kind)
    → λ(d : ./../Category/Kind kArrow kProduct vObject dObject)
    → λ(dCategory : ./../Category/Type dObject v d)
    → let object = kArrow cObject dObject
      
      let arrow = ./Type cObject dObject d
      
      in    { identity =
                λ(a : object) → λ(i : cObject) → dCategory.identity (a i)
            , op =
                  λ(a : { _1 : object, _2 : object })
                → λ ( p
                    :   ∀(r : Type)
                      → (   ∀(z : object)
                          → arrow { _1 = z, _2 = a._2 }
                          → arrow { _1 = a._1, _2 = z }
                          → r
                        )
                      → r
                    )
                → λ(i : cObject)
                → dCategory.op
                  { _1 = a._1 i, _2 = a._2 i }
                  (   λ(r : Type)
                    → λ ( arrowsOut
                        :   ∀(z : dObject)
                          → d.arrow { _1 = z, _2 = a._2 i }
                          → d.arrow { _1 = a._1 i, _2 = z }
                          → r
                        )
                    → p
                      r
                      (   λ(z : object)
                        → λ(f : arrow { _1 = z, _2 = a._2 })
                        → λ(g : arrow { _1 = a._1, _2 = z })
                        → arrowsOut (z i) (f i) (g i)
                      )
                  )
            }
          : ./../Category/Type
            object
            v
            ( ./../Category/Functor/category
              cObject
              dObject
              ( ./../Category/Monoidal/extractCategory
                kArrow
                kProduct
                Type
                vObject
                v
              )
              c
              d
            )
