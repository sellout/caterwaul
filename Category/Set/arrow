let Cat =
      ./../Cat/semigroupal sha256:de5b05c9640ea3ca0ee586c7102a982db79d3c0f0c92230cb2df19c7b986b513

let vObject = Type

let v =
      ./../Monoidal/Cartesian/extractMonoidal sha256:8112e73d82fd63e4e8bb5628a8cea43f92518a02e56ad351c7f9558546e7f81a
      Cat
      Type
      vObject
      ./monoidal/cartesian sha256:3a79a17d1c39a3df58efcc62fd96a2da0147bb29b524f57aa7c35942f653f164

let Set =
      ./../Monoidal/Cartesian/extractSemigroupal sha256:239a4e0b126d58dfe79bdd73558817e8c85697571aebbcb47c47e48e6202d82e
      Cat
      vObject
      Type
      ./monoidal/cartesian sha256:3a79a17d1c39a3df58efcc62fd96a2da0147bb29b524f57aa7c35942f653f164

in    { split =
            λ(a : Type)
          → λ(b : Type)
          → λ(c : Type)
          → λ(d : Type)
          → λ(f : a → b)
          → λ(g : c → d)
          → λ(p : { _1 : a, _2 : c })
          → { _1 = f p._1, _2 = g p._2 }
      , fanout =
            λ(a : Type)
          → λ(b : Type)
          → λ(c : Type)
          → λ(f : a → b)
          → λ(g : a → c)
          → λ(x : a)
          → { _1 = f x, _2 = g x }
      , unit =
          λ(a : Type) → λ(x : a) → x
      , product =
            λ(a : { _1 : Type, _2 : Type })
          → λ(p : ∀(r : Type) → (∀(z : Type) → (z → a._2) → (a._1 → z) → r) → r)
          → p
            (a._1 → a._2)
            (   λ(z : Type)
              → λ(f : z → a._2)
              → λ(g : a._1 → z)
              → λ(x : a._1)
              → f (g x)
            )
      , lmap =
            λ(a : Type)
          → λ(b : Type)
          → λ(c : Type)
          → λ(f : b → a)
          → λ(p : a → c)
          → λ(x : b)
          → p (f x)
      , rmap =
            λ(a : Type)
          → λ(b : Type)
          → λ(c : Type)
          → λ(f : b → c)
          → λ(p : a → b)
          → λ(x : a)
          → f (p x)
      , map =
            λ(a : { _1 : Type, _2 : Type })
          → λ(b : { _1 : Type, _2 : Type })
          → λ(f : { _1 : b._1 → a._1, _2 : a._2 → b._2 })
          → λ(fn : a._1 → a._2)
          → λ(x : b._1)
          → f._2 (fn (f._1 x))
      }
    : ./../../Arrow/Type sha256:cadeea2fc253655ecbc484a159bc5f8c4d32f9b26ef7ffe9aabda193c8d5fe0c
      Type
      v
      Set
