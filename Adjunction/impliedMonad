let kArrow = ./../Function/Kind

let kProduct = ./../Tuple/Kind

let Functor = ./../Functor/Type

let Adjunction = ./Type

let Monad = ./../Monad/Type

let Compose = ./../Compose/Type

let vObject = Type

let v =
      ./../Category/Monoidal/extractCategory
      kArrow
      kProduct
      Type
      vObject
      ./../Category/Set/monoidal/cartesian

let Category = ./../Category/Kind kArrow kProduct vObject

in    λ(cObject : Kind)
    → λ(dObject : Kind)
    → λ(c : Category cObject)
    → λ(d : Category dObject)
    → λ(f : kArrow dObject cObject)
    → λ(g : kArrow cObject dObject)
    → λ(functor : Functor vObject cObject dObject v c d g)
    → λ(adjunction : Adjunction cObject dObject c d f g)
    →   { identity =
            adjunction.unit
        , op =
              λ(a : dObject)
            → functor.map (f (g (f a))) (f a) (adjunction.counit (f a))
        }
      : Monad dObject d (Compose dObject cObject dObject { _1 = g, _2 = f })
