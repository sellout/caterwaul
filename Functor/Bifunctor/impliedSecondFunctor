let cat =
      ./../../Category/Cat/semigroupal sha256:de5b05c9640ea3ca0ee586c7102a982db79d3c0f0c92230cb2df19c7b986b513

let vObject = Type

let v =
      ./../../Category/Monoidal/Cartesian/extractMonoidal sha256:25afdf2818357f9c4b9dac6652b82e730472c389a634f8417a6014d39c7cddc0
      cat
      Type
      vObject
      ./../../Category/Set/monoidal/cartesian sha256:48e8bea552638624a62335eab92acd1e0a04c28e8a0d8fe1c8974565065a9576

let Category =
      ./../../Category/Kind sha256:c73426d12cddcdfc44b6e687fc897f7ef91fcda0c4da116a91acdca4d15c68a2
      cat
      vObject

in    λ ( cObject
        : Kind
        )
    → λ(c : Category cObject)
    → λ ( cCategory
        : ./../../Category/Type sha256:5792bd3a482ad0d318aecda464bd952a6561dc1119bdc1f5db574cfc3b66e095
          cObject
          v
          c
        )
    → λ(dObject : Kind)
    → λ(d : Category dObject)
    → λ(eObject : Kind)
    → λ(e : Category eObject)
    → λ(f : cat.arrow (cat.product cObject dObject) eObject)
    → λ ( bifunctor
        : ./Type sha256:d8949693dfb049e8d09615a9e1e7037418bc3e654d2eeecfc61e41a99e7daf41
          vObject
          cObject
          dObject
          eObject
          v
          c
          d
          e
          f
        )
    → λ(a : cObject)
    →   { map =
              λ(b : dObject)
            → λ(c : dObject)
            → λ(fn : d.arrow { _1 = b, _2 = c })
            → bifunctor.map
              { _1 = a, _2 = b }
              { _1 = a, _2 = c }
              { _1 = cCategory.identity a, _2 = fn }
        }
      : ./../Type sha256:bebd999f32d6a3fd9bd7151ca5f0942ed6aeded30809c4a7e6c91382bed2dbcb
        vObject
        dObject
        eObject
        ( ./../../Category/Monoidal/extractCategory sha256:936b393338b8dcf60af6c25ecea2b0b02d83e18acfed60ff736754fe0c1d7b3d
          cat
          Type
          vObject
          v
        )
        d
        e
        (λ(b : dObject) → f { _1 = a, _2 = b })
