let Cat =
      ./../Cat/semigroupal sha256:de5b05c9640ea3ca0ee586c7102a982db79d3c0f0c92230cb2df19c7b986b513

let vObject = Type

let v =
      ./../Monoidal/Cartesian/extractMonoidal sha256:25afdf2818357f9c4b9dac6652b82e730472c389a634f8417a6014d39c7cddc0
      Cat
      Type
      vObject
      ./monoidal/cartesian sha256:48e8bea552638624a62335eab92acd1e0a04c28e8a0d8fe1c8974565065a9576

let Set =
      ./../Monoidal/Cartesian/extractSemigroupal sha256:7e5d21f3576c085dca88a9b3bfa2cc01b21136397fbb7c9a6ffd9a990c0282cf
      Cat
      vObject
      Type
      ./monoidal/cartesian sha256:48e8bea552638624a62335eab92acd1e0a04c28e8a0d8fe1c8974565065a9576

in    { split =
            λ(a : Type)
          → λ(b : Type)
          → λ(c : Type)
          → λ(d : Type)
          → λ(f : a → b)
          → λ(g : c → d)
          → λ(p : { _1 : a, _2 : c })
          → { _1 = f p._1, _2 = g p._2 }
      , fanout =
            λ(a : Type)
          → λ(b : Type)
          → λ(c : Type)
          → λ(f : a → b)
          → λ(g : a → c)
          → λ(x : a)
          → { _1 = f x, _2 = g x }
      , identity =
          λ(a : Type) → λ(x : a) → x
      , op =
            λ(a : { _1 : Type, _2 : Type })
          → λ(p : ∀(r : Type) → (∀(z : Type) → (z → a._2) → (a._1 → z) → r) → r)
          → p
            (a._1 → a._2)
            (   λ(z : Type)
              → λ(f : z → a._2)
              → λ(g : a._1 → z)
              → λ(x : a._1)
              → f (g x)
            )
      , lmap =
            λ(a : Type)
          → λ(b : Type)
          → λ(c : Type)
          → λ(f : b → a)
          → λ(p : a → c)
          → λ(x : b)
          → p (f x)
      , rmap =
            λ(a : Type)
          → λ(b : Type)
          → λ(c : Type)
          → λ(f : b → c)
          → λ(p : a → b)
          → λ(x : a)
          → f (p x)
      , map =
            λ(a : { _1 : Type, _2 : Type })
          → λ(b : { _1 : Type, _2 : Type })
          → λ(f : { _1 : b._1 → a._1, _2 : a._2 → b._2 })
          → λ(fn : a._1 → a._2)
          → λ(x : b._1)
          → f._2 (fn (f._1 x))
      }
    : ./../../Arrow/Type sha256:759b21e206149156890a66e451068ab35e69fd0bf99ad580084c390022c48008
      Type
      v
      Set
