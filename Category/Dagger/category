let kArrow =
      ./../../Function/Kind sha256:0b6372c593badac5fe880699fd1cf19dfba07c62fab935a8bad96915866f466e

let kProduct =
      ./../../Tuple/Kind sha256:03e1c52890f0cda1a2181cd3eb045d5344fc7c907fcbf836736c0e3222c5ffd8

let vObject = Type

in    λ ( object
        : Kind
        )
    → λ ( v
        : ./../../Category/Monoidal/Kind sha256:f05d8665b46c686b6b930ad18aad6e1daa8c7a9c104d7bbeec947389d4d83e48
          kArrow
          kProduct
          Type
          vObject
        )
    → λ ( cat
        : ./../Kind sha256:41ea86fd34226d0dc3c87cc12f9ce042e3b54e7eb77b451cdbfb085ba3f66916
          kArrow
          kProduct
          vObject
          object
        )
    → λ ( category
        : ./../Type sha256:b5245b1f92f65f1b72bbfac6c53ebc19dc3c778b775b19ea4ad73fa6d2569dcb
          object
          v
          cat
        )
    → let op =
            ./../Op/Kind sha256:e9734284b447322a4da84aed6be9d3c9df5ed65f9428ad99e6b519ebe13a4c0a
            vObject
            object
            cat
      
      let opCategory =
            ./../Op/category sha256:ac633f55487233f6be2887dee86a19f6eea400124b58e7571a8f3dc4d3a73ffc
            object
            v
            cat
            category
      
      let Dagger =
            ./Kind sha256:28f645c4639adb33671f8b0ef4c8fc20679cf28357b61be71cb415f10c6c7ae5
            object
            cat
      
      in    { identity =
                  λ(a : object)
                → { to = category.identity a, from = opCategory.identity a }
            , op =
                  λ(a : { _1 : object, _2 : object })
                → λ ( p
                    :   ∀(r : Type)
                      → (   ∀(z : object)
                          → Dagger.arrow { _1 = z, _2 = a._2 }
                          → Dagger.arrow { _1 = a._1, _2 = z }
                          → r
                        )
                      → r
                    )
                → { to =
                      category.op
                      a
                      (   λ(r : Type)
                        → λ ( arrowsOut
                            :   ∀(z : object)
                              → cat.arrow { _1 = z, _2 = a._2 }
                              → cat.arrow { _1 = a._1, _2 = z }
                              → r
                            )
                        → p
                          r
                          (   λ(z : object)
                            → λ(f : Dagger.arrow { _1 = z, _2 = a._2 })
                            → λ(g : Dagger.arrow { _1 = a._1, _2 = z })
                            → arrowsOut z f.to g.to
                          )
                      )
                  , from =
                      opCategory.op
                      a
                      (   λ(r : Type)
                        → λ ( arrowsOut
                            :   ∀(z : object)
                              → op.arrow { _1 = z, _2 = a._2 }
                              → op.arrow { _1 = a._1, _2 = z }
                              → r
                            )
                        → p
                          r
                          (   λ(z : object)
                            → λ(f : Dagger.arrow { _1 = z, _2 = a._2 })
                            → λ(g : Dagger.arrow { _1 = a._1, _2 = z })
                            → arrowsOut z f.from g.from
                          )
                      )
                  }
            }
          : ./../Type sha256:b5245b1f92f65f1b72bbfac6c53ebc19dc3c778b775b19ea4ad73fa6d2569dcb
            object
            v
            Dagger
