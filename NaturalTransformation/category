let cat =
      ./../Category/Cat/semigroupal sha256:de5b05c9640ea3ca0ee586c7102a982db79d3c0f0c92230cb2df19c7b986b513

let Category =
      ./../Category/Kind sha256:c73426d12cddcdfc44b6e687fc897f7ef91fcda0c4da116a91acdca4d15c68a2
      cat

let vObject = Type

in    λ ( v
        : ./../Category/Monoidal/Kind sha256:96b5eca83adb85b7bc744975b530210da2aec17c9c70b45d3b241ccfdb47e772
          cat
          Type
          vObject
        )
    → λ(cObject : Kind)
    → λ(c : Category vObject cObject)
    → λ(dObject : Kind)
    → λ(d : Category vObject dObject)
    → λ ( dCategory
        : ./../Category/Type sha256:a3968397f7c9a309d7c4eed545517a4e6f451eff463aaaf274fd1c7d37cb3004
          dObject
          v
          d
        )
    → let object =
            cat.arrow cObject dObject
      
      let arrow =
            ./Type sha256:ae103806c5d0da8f35ba65b9c08afdcc86ed1d5d71eb2ed4a0d631119df51bbc
            cObject
            dObject
            d
      
      in    { identity =
                λ(a : object) → λ(i : cObject) → dCategory.identity (a i)
            , product =
                  λ(a : { _1 : object, _2 : object })
                → λ ( p
                    :   ∀(r : Type)
                      → (   ∀(z : object)
                          → arrow { _1 = z, _2 = a._2 }
                          → arrow { _1 = a._1, _2 = z }
                          → r
                        )
                      → r
                    )
                → λ(i : cObject)
                → dCategory.product
                  { _1 = a._1 i, _2 = a._2 i }
                  (   λ(r : Type)
                    → λ ( arrowsOut
                        :   ∀(z : dObject)
                          → d.arrow { _1 = z, _2 = a._2 i }
                          → d.arrow { _1 = a._1 i, _2 = z }
                          → r
                        )
                    → p
                      r
                      (   λ(z : object)
                        → λ(f : arrow { _1 = z, _2 = a._2 })
                        → λ(g : arrow { _1 = a._1, _2 = z })
                        → arrowsOut (z i) (f i) (g i)
                      )
                  )
            }
          : ./../Category/Type sha256:a3968397f7c9a309d7c4eed545517a4e6f451eff463aaaf274fd1c7d37cb3004
            object
            v
            ( ./../Category/Functor/category sha256:c69163eac75892504c9b6fd8b9637704c20e0baa23c5d428ecfb2b4587414bef
              cObject
              dObject
              ( ./../Category/Monoidal/extractCategory sha256:be63455106b8fe90ee7366a02f05b29c1aa42e63243bb9994d55011052eab6ec
                cat
                Type
                vObject
                v
              )
              c
              d
            )
